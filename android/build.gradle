buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
    }
}

repositories {
    google()
    jcenter()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$projectDir/../../react-native/android"
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def _reactNativeVersion = safeExtGet('reactNative', '+')
def _compileSdkVersion = safeExtGet('compileSdkVersion', 27)
def _glideVersion = safeExtGet('glideVersion', '4.7.1')

android {
    compileSdkVersion _compileSdkVersion

    sourceSets {
        main {
            java {
                if (safeExtGet('excludeAppGlideModule', false)) {
                    srcDir 'src'
                    exclude '**/FastImageGlideModule.java'
                }
            }
        }
    }

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 27)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation("com.facebook.react:react-native:${_reactNativeVersion}") {
        exclude group: "com.android.support"
    }
    implementation "com.android.support:support-v4:${safeExtGet('supportLibVersion', '27.1.1')}"
    implementation("com.github.bumptech.glide:glide:${_glideVersion}") {
        exclude group: "com.android.support"
    }
    implementation("com.github.bumptech.glide:annotations:${_glideVersion}") {
        exclude group: "com.android.support"
    }
    annotationProcessor "com.github.bumptech.glide:compiler:${_glideVersion}"
    implementation("com.github.bumptech.glide:okhttp3-integration:${_glideVersion}") {
        exclude group: "com.android.support"
        exclude group: 'glide-parent'
    }
}
