buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.4.0"
    }
}

apply plugin: "com.android.library"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def _reactNativeVersion = safeExtGet("reactNative", "+")
def _compileSdkVersion = safeExtGet("compileSdkVersion", 28)
def _glideVersion = safeExtGet("glideVersion", "4.9.0")

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion safeExtGet("buildToolsVersion", "28.0.3")

    sourceSets {
        main {
            java {
                if (safeExtGet("excludeAppGlideModule", false)) {
                    srcDir "src"
                    exclude "**/FastImageGlideModule.java"
                }
            }
        }
    }

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 16)
        targetSdkVersion safeExtGet("targetSdkVersion", 28)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.google.com"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:${_reactNativeVersion}"
    implementation "androidx.core:core:1.0.0"
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "androidx.annotation:annotation:1.0.0"
    implementation("com.github.bumptech.glide:glide:${_glideVersion}")
    implementation("com.github.bumptech.glide:annotations:${_glideVersion}")
    annotationProcessor 'com.android.support:support-annotations:28.0.0-alpha3'
    annotationProcessor "com.github.bumptech.glide:compiler:${_glideVersion}"
    implementation("com.github.bumptech.glide:okhttp3-integration:${_glideVersion}")
}
